// Never Forget: Comments can "lie", but code not so much ;P
// -----------
// Information
// -----------
BACKUP 	~weidu_external/tutorils_he/backup~ // %MOD_FOLDER% doesn't work here
AUTHOR 	~Incrementis (incrementis[@]gmail.com)~
VERSION ~v0.1.10~
AUTO_EVAL_STRINGS// This will prevent the need of EVAL for string variables


// --------------
// Initialization
// --------------
ALWAYS 
	// LIBRARY INCLUDES(Don't touch these)
	// -----------------------------------
	INCLUDE ~%MOD_FOLDER%/lib/simp_AddItemHeader.tph~
	INCLUDE ~%MOD_FOLDER%/lib/warnings.tph~
	INCLUDE ~%MOD_FOLDER%/lib/errors.tph~
	INCLUDE ~%MOD_FOLDER%/lib/2daHandling.tph~
	INCLUDE ~%MOD_FOLDER%/lib/IdsHandling.tph~
	INCLUDE ~%MOD_FOLDER%/lib/dialogTlkFunctionalities.tph~
	// --- WINDOWS(Works in 64-bit version and possibly 32-bit version too)
	ACTION_IF (~%WEIDU_OS%~ STRING_EQUAL ~win32~) THEN
	BEGIN
		INCLUDE ~%MOD_FOLDER%/lib/windows/dynArrayFunctionalities.tph~
	END
	// --- MAC-OS
	ACTION_IF (~%WEIDU_OS%~ STRING_EQUAL ~osx~) THEN
	BEGIN
		INCLUDE ~%MOD_FOLDER%/lib/mac/dynArrayFunctionalities.tph~
	END
	// --- LINUX
	ACTION_IF (~%WEIDU_OS%~ STRING_EQUAL ~unix~) THEN
	BEGIN
		INCLUDE ~%MOD_FOLDER%/lib/linux/dynArrayFunctionalities.tph~
	END

END// End of "ALWAYS"


// ---------
// Languages
// ---------
LANGUAGE
	~English~	// Language
	~English~	// Directory
	~tutorils_he/English/!_global.tra~
	~tutorils_he/English/!_setup.tra~
	~tutorils_he/English/!_hoftd.tra~
	~tutorils_he/English/!_warnings.tra~
	~tutorils_he/English/!_errors.tra~

/*	
LANGUAGE
	~Deutsch~	// Language
	~German~	// Directory
	~tutorils_he/Deutsch/!_global.tra~
	~tutorils_he/Deutsch/!_setup.tra~
	~tutorils_he/Deutsch/!_hoftd.tra~
	~tutorils_he/Deutsch/!_warnings.tra~
	~tutorils_he/Deutsch/!_errors.tra~
*/

// ------------------------------------------------
// Install "Tutoril's Historical Encyclopedias" Mod
// ------------------------------------------------
BEGIN @1 LABEL "!_-TutorilsHistoricalEncyclopedias-Main" DESIGNATED 1000
// GLOBAL INCLUDES(Don't touch this!)
// ----------------------------------	
INCLUDE ~%MOD_FOLDER%/laf/errors/!_CheckIEgames.tpa~
INCLUDE ~%MOD_FOLDER%/lam/preprocessing/!_DeclareBookArrays.tpa~

// TODO FOLLOWING CODE: CHECK IF REFACTORING IS NEEDED
// ---------------------------------------------------
// Creates an array with book strefs(see dialog.tlk)
ACTION_DEFINE_ARRAY books_history_of_the_drow
BEGIN 
	~13165~
	~13166~
END
// Creates an array with sound file names
ACTION_DEFINE_ARRAY voces_history_of_the_drow
BEGIN 
	~!_HOTD01~
	~!_HOTD02~
END

// Concatenate all text from books
OUTER_SPRINT HOFTD_book_text ~~
OUTER_FOR (indexVocesBooks = 0; VARIABLE_IS_SET $books_history_of_the_drow(~%indexVocesBooks%~); ++indexVocesBooks) BEGIN
	// GETS DATA
	// ---------
	// Gets specific text strref from book
	OUTER_SPRINT book $books_history_of_the_drow(~%indexVocesBooks%~)
	// Gets the specific vox sound file from the list
	OUTER_SPRINT vox $voces_history_of_the_drow(~%indexVocesBooks%~)
	// Gets the text of the book from dialog.tlk
	ACTION_GET_STRREF ~%book%~ book_text
	// Gets new strref and writes the specifci book text and sound into the specific strref position in dialog.tlk
	LAF RESOLVE_STR_REF_WITH_STRINGS
		STR_VAR
			text 	= ~%book_text%~
			sound	= ~%vox%~
		RET
			stringref = strRef
	END
	// Stores string references for later insertion into the specific 2DA file
	OUTER_SET 		~!_position~ 	= indexVocesBooks
	OUTER_SET 		~!_maxSlots~ 	= HOFTD_numOfStrrefs
	OUTER_SET		~!_collected~	= 0
	OUTER_SPRINT 	~!_useArrayName~ ~HOFTD_strrefs~
	OUTER_SPRINT 	~!_arrayValue~	 ~%stringref%~
	LAM DYN_ARRAY_SET_VALUES
	// All values collected
	ACTION_IF(indexVocesBooks = (HOFTD_numOfStrrefs - 1)) THEN
	BEGIN
		// To be on the safe side, initilaizes all macros variables 
		// and calls macros DYN_ARRAY_SET_VALUES
		OUTER_SET 		~!_position~ 	=	indexVocesBooks
		OUTER_SET 		~!_maxSlots~ 	=	HOFTD_numOfStrrefs
		OUTER_SET		~!_collected~	=	1
		OUTER_SPRINT 	~!_useArrayName~ ~HOFTD_strrefs~
		OUTER_SPRINT 	~!_arrayValue~	 ~%stringref%~
		LAM DYN_ARRAY_SET_VALUES
	END

	// CONCATENATING
	// -------------
	// WINDOWS(Works in 64-bit version and possibly 32-bit version too)
	ACTION_IF (~%WEIDU_OS%~ STRING_EQUAL ~win32~) THEN
	BEGIN
		OUTER_SPRINT HOFTD_book_text ~%HOFTD_book_text%%WNL%~^~%WNL%%book_text%~
	END
	// MAC-OS
	ACTION_IF (~%WEIDU_OS%~ STRING_EQUAL ~osx~) THEN
	BEGIN
		OUTER_SPRINT HOFTD_book_text ~%HOFTD_book_text%%MNL%~^~%MNL%%book_text%~
	END
	// LINUX
	ACTION_IF (~%WEIDU_OS%~ STRING_EQUAL ~unix~) THEN
	BEGIN
		OUTER_SPRINT HOFTD_book_text ~%HOFTD_book_text%%LNL%~^~%LNL%%book_text%~
	END

	// MONITORING: TODO DELETE PRINTS!
	PRINT ~indexVocesBooks:%indexVocesBooks%~
	PRINT  ~HOFTD_book_text:%HOFTD_book_text%~
	OUTER_SPRINT test0 $~HOFTD_strrefs~(~%indexVocesBooks%~)
	PRINT ~strrefs-loop:%test0%~
	
END// End of "OUTER_FOR"
// MONITORING: TODO DELETE PRINTS!
OUTER_FOR(index = 0; index < 2; ++index) 
BEGIN
	OUTER_SPRINT test0 $~HOFTD_strrefs~(~%index%~)
	PRINT ~strrefs-loop:%test0%~
END
OUTER_SPRINT test0 $~HOFTD_strrefs~(0)
OUTER_SPRINT test1 $~HOFTD_strrefs~(1)
PRINT ~strrefs:%test0%,%test1%~	

// Gets the the stringref for the button which references to the button lable. 
OUTER_SET button = RESOLVE_STR_REF(@100)
// Compile d file to DLG and the tra file is needed to identifiy which tra file it should use?
COMPILE ~%MOD_FOLDER%/DLG/!_HOFTD.d~
// --------------------------------------------------------


// CREATING ITEM FILES
// -------------------
CREATE ~ITM~ ~!_HOFTD~	// "THE:History of the Drow"


// BAM FILES FOR ALL ITEMS
// -----------------------
COPY ~%MOD_FOLDER%/BAM/inventory~ 	~override~
COPY ~%MOD_FOLDER%/BAM/description~ ~override~
COPY ~%MOD_FOLDER%/BAM/ground~ 		~override~
// WAV FILES FOR ALL ITEMS
// -----------------------
COPY ~%MOD_FOLDER%/sound/inventory~ ~override~
COPY ~%MOD_FOLDER%/sound/sequence~ 	~override~
// MOS FILES FOR ALL ITEMS
// -----------------------
COPY ~%MOD_FOLDER%/MOS~ ~override~
// 2DA FILES FOR ALL ITEMS
// -------------------------------------------------------------------------------------------------------------
// TODO: CHECK IF REFACTORING IS NEEDED
// Makes item conversable(Item, text, dialog file)
APPEND ~ITEMDIAL.2DA~ ~!_HOFTD      %button%  !_HOFTD~
// Information for the Reading Sequence
COPY "%MOD_FOLDER%/2DA/!_RESQ00.2DA" override
REPLACE ~111~ ~History of the Drow~
REPLACE ~222~ ~%HOFTD_book_text%~


// ITEM CONSTANTS HEADER(Don't touch this!)
// ----------------------------------------
OUTER_SET ITM_SIGNATURE				= 0x0000
OUTER_SET ITM_VERSION				= 0x0004
OUTER_SET ITM_PRICE 				= 0x0034
OUTER_SET ITM_WEIGHT				= 0x004c
OUTER_SET ITM_LORE					= 0x0042
OUTER_SET ITM_MIN_STRENGTH			= 0x0026
OUTER_SET ITM_MIN_LEVEL				= 0x0024
OUTER_SET ITM_MAX_IN_STACK			= 0x0038 
OUTER_SET ITM_ENCHANTMENT			= 0x0060 
OUTER_SET ITM_FLAGS					= 0x0018
OUTER_SET ITM_TYPE					= 0x001c
OUTER_SET ITM_UNUSABLE_BY			= 0x001e
OUTER_SET ITM_UNUSABLE_KIT1			= 0x0029
OUTER_SET ITM_UNUSABLE_KIT2			= 0x002b
OUTER_SET ITM_UNUSABLE_KIT3			= 0x002d
OUTER_SET ITM_UNUSABLE_KIT4			= 0x002f
OUTER_SET ITM_WEAPON_PROFICIENCY	= 0x0031
OUTER_SET ITM_GROUND_ICON			= 0x0044
OUTER_SET ITM_DESCRIPTION_IMAGE		= 0x0058
OUTER_SET ITM_INVENTORY_ICON		= 0x003a
OUTER_SET ITM_ANIMATION				= 0x0022
OUTER_SET ITM_NUMBER_OF_ABILITIES	= 0x0068


// GLOBAL VARIABLES (Changable)
// ----------------------------	
OUTER_SPRINT signature 			 ~ITM~
OUTER_SPRINT version 			 ~V1~	
OUTER_SET price	 				= 10
OUTER_SET weight 				= 30
OUTER_SET lore 					= 10
OUTER_SET min_strength 			= 6
OUTER_SET min_level 			= 4
OUTER_SET max_in_stack 			= 1
OUTER_SET type 					= 0x14		// E.g. 0x14 = value 20 for category "Large Swords"
OUTER_SET weapon_proficiency 	= 0x5E		// E.g. Katana
OUTER_SET animation		 		= 0x3353	// E.g. S3-Katana
// Binaries result in "Droppable", "Displayable", "Not copyable", "Magical"
OUTER_SET flags 				= 0b00000000000000000000100001101100
// Next five variables result in item only usable by Wizard Slayer
OUTER_SET unusable_by			= 0b01100000011111111111011111000000
OUTER_SET unusable_kit1			= 0b11111111
OUTER_SET unusable_kit2			= 0b11111111
OUTER_SET unusable_kit3			= 0b11111111
OUTER_SET unusable_kit4			= 0b11111101


// PATCHING ITEM FILE "THE:History of the Drow"
// -------------------------------------------
COPY_EXISTING ~!_HOFTD.ITM~ override
	// LOCAL INCLUDES(Content changeable)
	// ----------------------------------
	PATCH_INCLUDE ~%MOD_FOLDER%/lpf/!_HistoryOfTheDrow.tpp~

	// LOCAL VARIABLES
	// ---------------
	SET L_enchantment = 1
	SPRINT L_ground_icon 		~!_GHOFTD~				
	SPRINT L_description_image 	~!_CHOFTD~
	SPRINT L_inventory_icon 	~!_IHOFTD~
	// Concatenates charge ability description with book text
	SPRINT L_description_text 	@5	
	SPRINT L_description_text	~%HOFTD_book_text%~^~%L_description_text%~

	// ITEM PROPERTIES (HEADER)
	// ------------------------
	SAY NAME1 				@2 									// Unidentified item name
	SAY NAME2 				@3 									// Identified item name 		
	SAY UNIDENTIFIED_DESC 	@4 									// Unidentified description	
	SAY DESC 							~%L_description_text%~	// Identified description				
	WRITE_ASCII	ITM_SIGNATURE			~%signature%~
	WRITE_ASCII	ITM_VERSION				~%version%~		
	WRITE_ASCII	ITM_GROUND_ICON			~%L_ground_icon%~		
	WRITE_ASCII	ITM_DESCRIPTION_IMAGE	~%L_description_image%~		
	WRITE_ASCII	ITM_INVENTORY_ICON		~%L_inventory_icon%~
	WRITE_LONG 	ITM_PRICE				price
	WRITE_LONG 	ITM_FLAGS				flags
//	WRITE_LONG 	ITM_UNUSABLE_BY			unusable_by
	WRITE_SHORT ITM_WEIGHT 				weight 	
	WRITE_SHORT ITM_LORE 				lore 
	WRITE_SHORT ITM_TYPE				L_newtype
//	WRITE_SHORT ITM_ANIMATION			animation	
//	WRITE_BYTE	ITM_MIN_STRENGTH 		min_strength
//	WRITE_BYTE 	ITM_MIN_LEVEL	 		min_level
//	WRITE_BYTE	ITM_MAX_IN_STACK	 	max_in_stack
	WRITE_BYTE	ITM_ENCHANTMENT	 		L_enchantment
//	WRITE_BYTE	ITM_WEAPON_PROFICIENCY	weapon_proficiency
//	WRITE_BYTE 	ITM_UNUSABLE_KIT1		unusable_kit1	
//	WRITE_BYTE 	ITM_UNUSABLE_KIT2		unusable_kit2	
//	WRITE_BYTE 	ITM_UNUSABLE_KIT3		unusable_kit3	
//	WRITE_BYTE 	ITM_UNUSABLE_KIT4		unusable_kit4
BUT_ONLY // End patching: ~!_HOFTD.ITM~

// PATCHING STORE FILE "Feldepost's Inn"(Beregost)
// -----------------------------------------------
COPY_EXISTING ~INN3351.STO~ ~override~
	// LOCAL INCLUDES(Content changeable)
	// ----------------------------------
	PATCH_INCLUDE ~%MOD_FOLDER%/lpf/!_StoreAllItems.tpp~
BUT_ONLY // End patching: ~INN3351.STO~


// FOOTER INCLUDES
// ---------------
INCLUDE ~%MOD_FOLDER%/laf/warnings/!_CheckSOD.tpa~
INCLUDE ~%MOD_FOLDER%/laf/warnings/!_CheckIEgames.tpa~