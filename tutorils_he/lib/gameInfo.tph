// Never Forget: Comments can "lie", but code not so much ;P
//-------------------------------------------------------------//
// Contains self defined operations regarding game information //
//-------------------------------------------------------------//



// Purpose: Retrieves the game identifier or a group identifier as a string.
// -------------------------------------------------------------------------
// Parameters:|
// ------------
// asGroup		->  Integer value (default: 0).
//              	0: Returns a single game identifier.
//              	1: Returns a grouped game identifier (e.g., "iwd" for iwd, how, and totlm).
//              	See the code for specific group combinations.  These combinations are based on
//              	game checks found in: https://github.com/Gibberlings3/modding-resources/blob/master/code/cam/game_checks.tpa
// identifier 	-> 	Returns the found identifier as a string
DEFINE_DIMORPHIC_FUNCTION ~GET_GAME_IDENTIFIER~
	INT_VAR
		asGroup = 0
	RET
		identifier
BEGIN
	// Checks if asGroup is too large
	ACTION_IF (~%asGroup%~ > 1) THEN 
	BEGIN
		FAIL ~DEFINE_DIMORPHIC_FUNCTION GET_GAME_IDENTIFIER -> The value %asGroup% for the parameter asGroup is larger than 1. Valid values are 0 and 1. A value of 0 retrieves a single game identifier, while 1 retrieves a grouped identifier.~
	END
	// Checks if asGroup is too small
	ACTION_IF (~%asGroup%~ < 0) THEN
	BEGIN
		FAIL ~DEFINE_DIMORPHIC_FUNCTION GET_GAME_IDENTIFIER -> The value %asGroup% for the parameter asGroup is smaller than 1. Valid values are 0 and 1. A value of 0 retrieves a single game identifier, while 1 retrieves a grouped identifier.~
	END

	// INITIALIZES VARIABLES
	// ---------------------
	OUTER_SPRINT identifier ~!_NULL~
	OUTER_SET NoSoD = 1
	// Defines array of game identifiers
	ACTION_DEFINE_ARRAY gameIdentifiers
	BEGIN
		~bg1~
		~totsc~
		~soa~
		~tob~
		~iwd~
		~how~
		~totlm~ 
		~iwd2~ 
		~pst~ 
		~bgee~ 
		~bg2ee~ 
		~iwdee~ 
		~pstee~ 
		~tutu~ 
		~tutu_totsc~ 
		~bgt~ 
		~eet~ 
		~ca~ 
		~iwd-in-bg2~
	END
	
	// Checks for SoD installation
	ACTION_IF (GAME_INCLUDES ~sod~ AND ~%asGroup%~ == 0) THEN
	BEGIN
		OUTER_SPRINT 	identifier 	~sod~
		OUTER_SET 		NoSoD 		= 0
	END
	

	// Process game identifiers (skipped if SoD is installed)
	ACTION_IF (NoSoD == 1) THEN
	BEGIN
		OUTER_FOR (index = 0; VARIABLE_IS_SET $gameIdentifiers(~%index%~); ++index) BEGIN
			// This variable is needed as it seems that $gameIdentifiers(~%index%~) 
			// does not work as an if-condition
			OUTER_SPRINT tempIdentifier $gameIdentifiers(~%index%~)
			
			// --- Handles single identifier retrieval
			ACTION_IF (~%asGroup%~ == 0) THEN
			BEGIN
				ACTION_IF (GAME_IS ~%tempIdentifier%~) THEN
				BEGIN
					OUTER_SPRINT identifier $gameIdentifiers(~%index%~)
				END 				
			END // End of first if statement
			
			// --- Handles grouped identifier retrieval
			ACTION_IF (~%asGroup%~ == 1) THEN
			BEGIN
				// Group: IWD
				ACTION_IF (	GAME_IS ~iwd~ OR 
							GAME_IS ~how~ OR
							GAME_IS ~totlm~) THEN
				BEGIN
					OUTER_SPRINT identifier ~iwd~
				END
				// Group: BG1
				ACTION_IF (	GAME_IS ~bg1~ OR 
							GAME_IS ~totsc~ ) THEN
				BEGIN
					OUTER_SPRINT identifier ~bg1~
				END
				// Group: BG2
				ACTION_IF (	GAME_IS ~soa~ OR 
							GAME_IS ~tob~ ) THEN
				BEGIN
					OUTER_SPRINT identifier ~bg2~
				END
				// Group: TUTU_GEN
				ACTION_IF (	GAME_IS ~tutu~ OR 
							GAME_IS ~tutu_totsc~ ) THEN
				BEGIN
					OUTER_SPRINT identifier ~tutu_gen~
				END
				// Group: EE
				ACTION_IF (	GAME_IS ~bgee~ 	OR 
							GAME_IS ~bg2ee~	OR
							GAME_IS ~iwdee~	OR
							GAME_IS ~eet~	OR
							GAME_IS ~pstee~	) THEN
				BEGIN
					OUTER_SPRINT identifier ~ee~
				END			
			END // End of second if statement
			
		END // End of for loop		
	END // End of main if-statement
	
	// Checks if no identifier is found for asGroup = 1
	ACTION_IF ( (~%identifier%~ STRING_EQUAL ~!_NULL~) AND (~%asGroup%~ == 1) ) THEN
	BEGIN
		FAIL ~DEFINE_DIMORPHIC_FUNCTION GET_GAME_IDENTIFIER -> No grouped identifier found. Ensure the game is part of a defined group (e.g., IWD, BG1, BG2). If a single identifier was intended, set asGroup to 0.~
	END
	// Checks if no identifier found at all
	ACTION_IF (~%identifier%~ STRING_EQUAL ~!_NULL~) THEN
	BEGIN
		FAIL ~DEFINE_DIMORPHIC_FUNCTION GET_GAME_IDENTIFIER -> No identifier found. This indicates an internal error within the GET_GAME_IDENTIFIER function.~
	END
	
END// End of "GET_GAME_IDENTIFIER"
