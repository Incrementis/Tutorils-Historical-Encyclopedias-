// Never Forget: Comments can "lie", but code not so much ;P

// INIT ARRAYS
// -----------
// Creates an array with book strefs(see dialog.tlk)
ACTION_DEFINE_ARRAY books_history_of_the_drow
BEGIN
	~-1~
	~13165~
	~13166~
END
// Creates an array with sound file names
ACTION_DEFINE_ARRAY voces_history_of_the_drow
BEGIN
	~!_NULL~
	~!_HOTD00~
	~!_HOTD01~
END

ACTION_IF (GAME_IS ~iwdee~) THEN 
BEGIN
	// MAIN LOOP
	// ---------
	// Concatenates Book texts and store string references. 
	// The first value in the book array is -1 and is therefore skipped.
	OUTER_SPRINT HOFTD_book_text ~~
	OUTER_FOR (indexVocesBooks = 1; VARIABLE_IS_SET $books_history_of_the_drow(~%indexVocesBooks%~); ++indexVocesBooks)
	BEGIN
		OUTER_SET strrefBookIWDEE = $books_history_of_the_drow(~%indexVocesBooks%~)
		ACTION_GET_STRREF ~%strrefBookIWDEE%~ textBookIWDEE
		// Prevents line breaks at the beginning of the book text
		ACTION_IF (indexVocesBooks == 1) THEN
		BEGIN
			OUTER_SPRINT HOFTD_book_text ~%HOFTD_book_text%~^~%textBookIWDEE%~
		END ELSE
		BEGIN
			OUTER_SPRINT HOFTD_book_text ~%HOFTD_book_text%~^~%NL%%NL%%NL%%NL%~^~%textBookIWDEE%~
		END
	END
	// Writes the encyclopedia text in dialog.tlk and gets a strref from it
	LAF RESOLVE_STR_REF_WITH_STRINGS
		STR_VAR
			text 	= ~%HOFTD_book_text%~
			sound	= ~!_HOTD~
		RET
			HOFTD_strrefIWDEE = strRef
	END

	// This is required to create a 2da read sequence
	ACTION_DEFINE_ARRAY HOFTD_strrefsIWDEE
	BEGIN
		~-1~
		~%HOFTD_strrefIWDEE%~
	END
END ELSE
BEGIN
	// MAIN LOOP
	// ---------
	// Concatenates Book texts and store string references
	OUTER_SPRINT HOFTD_book_text ~~
	OUTER_FOR (indexVocesBooks = 0; VARIABLE_IS_SET $books_history_of_the_drow(~%indexVocesBooks%~); ++indexVocesBooks) BEGIN
		// FETCHES BOOK DATA
		// -----------------
		// Gets specific text strref from book
		OUTER_SPRINT book $books_history_of_the_drow(~%indexVocesBooks%~)
		// Gets the specific vox sound file from the list
		OUTER_SPRINT vox $voces_history_of_the_drow(~%indexVocesBooks%~)
		// Gets the text of the book from dialog.tlk
		ACTION_GET_STRREF ~%book%~ book_text
		
		// Only valid values ​​are written to dialog.tlk
		ACTION_IF NOT(~%vox%~ STRING_EQUAL ~!_NULL~) THEN
		BEGIN
			// COLLECTS NEW VALUES
			// -------------------
			ACTION_IF(indexVocesBooks < (HOFTD_numOfStrrefs - 1))
			BEGIN
				// Gets new strref and writes the specifci book text and sound into the specific strref position in dialog.tlk
				// All books except the last book in encyclopedia
				LAF RESOLVE_STR_REF_WITH_STRINGS
					STR_VAR
						text 	= ~%book_text%%NL%%NL%%NL%%NL%___________~
						sound	= ~%vox%~
					RET
						stringref = strRef
				END
			END

			// Stores string references for later insertion into the specific 2DA file
			OUTER_SET 		~!_position~ 	= indexVocesBooks
			OUTER_SET 		~!_maxSlots~ 	= HOFTD_numOfStrrefs // See file !_declareBookArrays
			OUTER_SET		~!_collected~	= 0
			OUTER_SPRINT 	~!_useArrayName~ ~HOFTD_strrefs~
			OUTER_SPRINT 	~!_arrayValue~	 ~%stringref%~
			LAM DYN_ARRAY_SET_VALUES

			// CONCATENATES TEXT FOR THE ENCYCLOPEDIA (description)
			// ----------------------------------------------------
			ACTION_IF (indexVocesBooks == 1) THEN
			BEGIN
				OUTER_SPRINT HOFTD_book_text ~%HOFTD_book_text%~^~%book_text%~
			END ELSE
			BEGIN
				OUTER_SPRINT HOFTD_book_text ~%HOFTD_book_text%~^~%NL%%NL%%NL%%NL%~^~%book_text%~
			END
		END ELSE
		BEGIN
			// The first strref in the 2DA column is for the title, which is not needed here and is therefore set to "-1".
			OUTER_SET 		~!_position~ 	= indexVocesBooks
			OUTER_SET 		~!_maxSlots~ 	= HOFTD_numOfStrrefs // See file !_declareBookArrays
			OUTER_SET		~!_collected~	= 0
			OUTER_SPRINT 	~!_useArrayName~ ~HOFTD_strrefs~
			OUTER_SPRINT 	~!_arrayValue~	 ~%book%~
			LAM DYN_ARRAY_SET_VALUES
		END

		// ALL VALUES COLLECTED
		// --------------------
		ACTION_IF(indexVocesBooks = (HOFTD_numOfStrrefs - 1)) THEN
		BEGIN
			// Gets new strref and writes the specifci book text and sound into the specific strref position in dialog.tlk
			// The last book in the encyclopedia
			LAF RESOLVE_STR_REF_WITH_STRINGS
				STR_VAR
					text 	= ~%book_text%%NL%%NL%%NL%%NL%FINIS~
					sound	= ~%vox%~
				RET
					stringref = strRef
			END

			// To be on the safe side, initilaizes all macros variables 
			// and calls macros DYN_ARRAY_SET_VALUES
			OUTER_SET 		~!_position~ 	=	indexVocesBooks
			OUTER_SET 		~!_maxSlots~ 	=	HOFTD_numOfStrrefs // See file !_declareBookArrays
			OUTER_SET		~!_collected~	=	1
			OUTER_SPRINT 	~!_useArrayName~ ~HOFTD_strrefs~
			OUTER_SPRINT 	~!_arrayValue~	 ~%stringref%~
			LAM DYN_ARRAY_SET_VALUES
		END
		
	END// End of "OUTER_FOR"
END// End of "ELSE"