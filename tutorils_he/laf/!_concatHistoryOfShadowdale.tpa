// Never Forget: Comments can "lie", but code not so much ;P

// INIT ARRAYS
// -----------
// Creates an array with book strefs(see dialog.tlk)
ACTION_DEFINE_ARRAY books_history_of_shadowdale
BEGIN
	~-1~ 
	~13146~
	~13147~
	~13148~
	~13149~
	~13150~
	~13151~
	~13152~
	~13153~
	~13154~
	~13155~
	~13156~
	~13157~
	~13158~
END
// Creates an array with sound file names
ACTION_DEFINE_ARRAY voces_history_of_the_shadowdale
BEGIN
	~!_NULL~
	~!_HOFS00~
	~!_HOFS01~
	~!_HOFS02~
	~!_HOFS03~
	~!_HOFS04~
	~!_HOFS05~
	~!_HOFS06~
	~!_HOFS07~
	~!_HOFS08~
	~!_HOFS09~
	~!_HOFS10~
	~!_HOFS11~
	~!_HOFS12~
END

ACTION_IF (GAME_IS ~iwdee~) THEN 
BEGIN
	// MAIN LOOP
	// ---------
	// Concatenates Book texts and store string references. 
	// The first value in the book array is -1 and is therefore skipped.
	OUTER_SPRINT HOFS_book_text ~~
	OUTER_FOR (indexVocesBooks = 1; VARIABLE_IS_SET $books_history_of_shadowdale(~%indexVocesBooks%~); ++indexVocesBooks)
	BEGIN
		OUTER_SET strrefBookIWDEE = $books_history_of_shadowdale(~%indexVocesBooks%~)
		ACTION_GET_STRREF ~%strrefBookIWDEE%~ textBookIWDEE
		// Prevents line breaks at the beginning of the book text
		ACTION_IF (indexVocesBooks == 1) THEN
		BEGIN
			OUTER_SPRINT HOFS_book_text ~%HOFS_book_text%~^~%textBookIWDEE%~
		END ELSE
		BEGIN
			OUTER_SPRINT HOFS_book_text ~%HOFS_book_text%~^~%NL%%NL%%NL%%NL%~^~%textBookIWDEE%~
		END
	END
	// Writes the encyclopedia text in dialog.tlk and gets a strref from it
	LAF RESOLVE_STR_REF_WITH_STRINGS
		STR_VAR
			text 	= ~%HOFS_book_text%~
			sound	= ~!_HOFS~
		RET
			HOFS_strrefIWDEE = strRef
	END

	// This is required to create a 2da read sequence
	ACTION_DEFINE_ARRAY HOFS_strrefsIWDEE
	BEGIN
		~-1~
		~%HOFS_strrefIWDEE%~
	END

END ELSE
BEGIN
	// MAIN LOOP
	// ---------
	// Concatenates Book texts and store string references
	OUTER_SPRINT HOFS_book_text ~~
	OUTER_FOR (indexVocesBooks = 0; VARIABLE_IS_SET $books_history_of_shadowdale(~%indexVocesBooks%~); ++indexVocesBooks) BEGIN
		// FETCHES BOOK DATA
		// -----------------
		// Gets specific text strref from book
		OUTER_SPRINT book $books_history_of_shadowdale(~%indexVocesBooks%~)
		// Gets the specific vox sound file from the list
		OUTER_SPRINT vox $voces_history_of_the_shadowdale(~%indexVocesBooks%~)
		// Gets the text of the book from dialog.tlk
		ACTION_GET_STRREF ~%book%~ book_text
		
		// Only valid values ​​are written to dialog.tlk
		ACTION_IF NOT(~%vox%~ STRING_EQUAL ~!_NULL~) THEN
		BEGIN
			// COLLECTS NEW VALUES
			// -------------------
			ACTION_IF(indexVocesBooks < (HOFS_numOfStrrefs - 1))
			BEGIN
				// Gets new strref and writes the specifci book text and sound into the specific strref position in dialog.tlk
				LAF RESOLVE_STR_REF_WITH_STRINGS
					STR_VAR
						text 	= ~%book_text%%NL%%NL%%NL%%NL%___________~
						sound	= ~%vox%~
					RET
						stringref = strRef
				END
			END
			
			// Stores string references for later insertion into the specific 2DA file
			OUTER_SET 		~!_position~ 	= indexVocesBooks
			OUTER_SET 		~!_maxSlots~ 	= HOFS_numOfStrrefs // See file !_declareBookArrays
			OUTER_SET		~!_collected~	= 0
			OUTER_SPRINT 	~!_useArrayName~ ~HOFS_strrefs~
			OUTER_SPRINT 	~!_arrayValue~	 ~%stringref%~
			LAM DYN_ARRAY_SET_VALUES

			// CONCATENATES TEXT FOR THE ENCYCLOPEDIA (description)
			// ----------------------------------------------------
			ACTION_IF (indexVocesBooks == 1) THEN
			BEGIN
				OUTER_SPRINT HOFS_book_text ~%HOFS_book_text%~^~%book_text%~
			END ELSE
			BEGIN
				OUTER_SPRINT HOFS_book_text ~%HOFS_book_text%~^~%NL%%NL%%NL%%NL%~^~%book_text%~
			END
					
		END ELSE
		BEGIN
			// The first strref in the 2DA column is for the title, which is not needed here and is therefore set to "-1".
			OUTER_SET 		~!_position~ 	= indexVocesBooks
			OUTER_SET 		~!_maxSlots~ 	= HOFS_numOfStrrefs // See file !_declareBookArrays
			OUTER_SET		~!_collected~	= 0
			OUTER_SPRINT 	~!_useArrayName~ ~HOFS_strrefs~
			OUTER_SPRINT 	~!_arrayValue~	 ~%book%~
			LAM DYN_ARRAY_SET_VALUES
		END

		// ALL VALUES COLLECTED
		// --------------------
		ACTION_IF(indexVocesBooks == (HOFS_numOfStrrefs - 1)) THEN
		BEGIN
			// Gets new strref and writes the specifci book text and sound into the specific strref position in dialog.tlk
			LAF RESOLVE_STR_REF_WITH_STRINGS
				STR_VAR
					text 	= ~%book_text%~
					sound	= ~%vox%~
				RET
					stringref = strRef
			END

			// To be on the safe side, initilaizes all macros variables 
			// and calls macros DYN_ARRAY_SET_VALUES
			OUTER_SET 		~!_position~ 	=	indexVocesBooks
			OUTER_SET 		~!_maxSlots~ 	=	HOFS_numOfStrrefs // See file !_declareBookArrays
			OUTER_SET		~!_collected~	=	1
			OUTER_SPRINT 	~!_useArrayName~ ~HOFS_strrefs~
			OUTER_SPRINT 	~!_arrayValue~	 ~%stringref%~
			LAM DYN_ARRAY_SET_VALUES
		END
		
	END// End of "OUTER_FOR"
END// End of "ELSE"