// Never Forget: Comments can "lie", but code not so much ;P

// INIT ARRAYS
// -----------
// Creates an array with book strefs(see dialog.tlk)
ACTION_DEFINE_ARRAY books_history_of_the_north
BEGIN
	~-1~ 
	~13170~
	~13171~
	~13172~
	~13173~
	~13174~
	~13175~
	~13176~
	~13177~
	~13178~
	~13179~
END
// Creates an array with sound file names
ACTION_DEFINE_ARRAY voces_history_of_the_north
BEGIN
	~!_NULL~
	~!_HOTN00~
	~!_HOTN01~
	~!_HOTN02~
	~!_HOTN03~
	~!_HOTN04~
	~!_HOTN05~
	~!_HOTN06~
	~!_HOTN07~
	~!_HOTN08~
	~!_HOTN09~
END


// MAIN LOOP
// ---------
// Concatenates Book texts and store string references
OUTER_SPRINT HOFTN_book_text ~~
OUTER_FOR (indexVocesBooks = 0; VARIABLE_IS_SET $books_history_of_the_north(~%indexVocesBooks%~); ++indexVocesBooks) BEGIN
	// FETCHES BOOK DATA
	// -----------------
	// Gets specific text strref from book
	OUTER_SPRINT book $books_history_of_the_north(~%indexVocesBooks%~)
	// Gets the specific vox sound file from the list
	OUTER_SPRINT vox $voces_history_of_the_north(~%indexVocesBooks%~)
	// Gets the text of the book from dialog.tlk
	ACTION_GET_STRREF ~%book%~ book_text
	
	// Only valid values ​​are written to dialog.tlk
	ACTION_IF NOT(~%vox%~ STRING_EQUAL ~!_NULL~) THEN
	BEGIN
		// COLLECTS NEW VALUES
		// -------------------
		// Gets new strref and writes the specifci book text and sound into the specific strref position in dialog.tlk
		LAF RESOLVE_STR_REF_WITH_STRINGS
			STR_VAR
				text 	= ~%book_text%~
				sound	= ~%vox%~
			RET
				stringref = strRef
		END
		// Stores string references for later insertion into the specific 2DA file
		OUTER_SET 		~!_position~ 	= indexVocesBooks
		OUTER_SET 		~!_maxSlots~ 	= HOFTN_numOfStrrefs
		OUTER_SET		~!_collected~	= 0
		OUTER_SPRINT 	~!_useArrayName~ ~HOFTN_strrefs~
		OUTER_SPRINT 	~!_arrayValue~	 ~%stringref%~
		LAM DYN_ARRAY_SET_VALUES

		// CONCATENATES TEXT FOR THE ENCYCLOPEDIA
		// --------------------------------------
		// WINDOWS(Works in 64-bit version and possibly 32-bit version too)
		ACTION_IF (~%WEIDU_OS%~ STRING_EQUAL ~win32~) THEN
		BEGIN
			// Adjust line endings(WNL)
			OUTER_SPRINT HOFTN_book_text ~%HOFTN_book_text%%WNL%~^~%WNL%%book_text%~
		END
		// MAC-OS
		ACTION_IF (~%WEIDU_OS%~ STRING_EQUAL ~osx~) THEN
		BEGIN
			// Adjust line endings(MNL)
			OUTER_SPRINT HOFTN_book_text ~%HOFTN_book_text%%MNL%~^~%MNL%%book_text%~
		END
		// LINUX
		ACTION_IF (~%WEIDU_OS%~ STRING_EQUAL ~unix~) THEN
		BEGIN
			// Adjust line endings(LNL)
			OUTER_SPRINT HOFTN_book_text ~%HOFTN_book_text%%LNL%~^~%LNL%%book_text%~
		END
	END ELSE
	BEGIN
		// The first strref in the 2DA column is for the title, which is not needed here and is therefore set to "-1".
		OUTER_SET 		~!_position~ 	= indexVocesBooks
		OUTER_SET 		~!_maxSlots~ 	= HOFTN_numOfStrrefs
		OUTER_SET		~!_collected~	= 0
		OUTER_SPRINT 	~!_useArrayName~ ~HOFTN_strrefs~
		OUTER_SPRINT 	~!_arrayValue~	 ~%book%~
		LAM DYN_ARRAY_SET_VALUES
	END

	// ALL VALUES COLLECTED
	// --------------------
	ACTION_IF(indexVocesBooks = (HOFTN_numOfStrrefs - 1)) THEN
	BEGIN
		// To be on the safe side, initilaizes all macros variables 
		// and calls macros DYN_ARRAY_SET_VALUES
		OUTER_SET 		~!_position~ 	=	indexVocesBooks
		OUTER_SET 		~!_maxSlots~ 	=	HOFTN_numOfStrrefs
		OUTER_SET		~!_collected~	=	1
		OUTER_SPRINT 	~!_useArrayName~ ~HOFTN_strrefs~
		OUTER_SPRINT 	~!_arrayValue~	 ~%stringref%~
		LAM DYN_ARRAY_SET_VALUES
	END
	
END// End of "OUTER_FOR"


// CREATE READINGSEQUENCE
// ----------------------
// Creates a "THE:History of the North" 2da file for reading sequence(Default-folder: override)
LAF CREATE_READINGSEQUENCE_2DA
	STR_VAR
		2daFilename		= "!_RESQ02"
		bgImage 		= "!_RESQ02"	// See MOS-folder
		strrefArray 	= ~HOFTN_strrefs~ 
END